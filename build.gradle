// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        compose_bom_version = "2023.01.00"
        compose_compiler_version = '1.3.2'
        kotlin_version = "1.7.20"
        androidx_appcompat_version = "1.5.1"
        androidx_activity_compose_version = "1.6.0"
        accompanist_version = "0.28.0"
        coil_version = '2.2.2'
    }
    repositories {
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.3.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.betomorrow.gradle:appcenter-plugin:2.0.3"
    }
}

plugins {
    id 'org.jetbrains.kotlin.android' version '1.5.21' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
    id("io.gitlab.arturbosch.detekt").version("1.18.1")
}

detekt {
    input = files(rootProject.rootDir)
    config = files("$projectDir/detekt.yml")
    failFast = true
    buildUponDefaultConfig = true
    basePath = projectDir
    reports {
        html.enabled = true
        xml.enabled = true
        sarif.enabled = true
        xml.destination = file("$buildDir/reports/detekt/detekt-checkstyle.xml")
        html.destination = file("$buildDir/reports/detekt/detekt-report.html")
        sarif.destination = file("$buildDir/reports/detekt/detekt-report.sarif")
    }
}

allprojects {

    group = 'com.telefonica.mistica'
    if (System.getProperty("SNAPSHOT_VERSION") != null) {
        version = System.getProperty("SNAPSHOT_VERSION")+"-SNAPSHOT"
    } else {
        version = System.getProperty("LIBRARY_VERSION") ?: "undefined"
    }

    repositories {
        google()
        mavenCentral()
    }
}

apply from: "${rootProject.projectDir}/publish_maven_central.gradle"