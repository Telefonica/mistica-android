apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
    }

    buildFeatures {
        dataBinding true
    }

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

dependencies {
    api "com.google.android.material:material:$support_version"
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    api 'androidx.constraintlayout:constraintlayout-solver:2.0.4'
    api 'androidx.recyclerview:recyclerview:1.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.airbnb.android:lottie:3.2.2'

    testImplementation 'junit:junit:4.13'
}

publishing {
    repositories {
        maven {
            credentials {
                username System.env.NEXUS_USER
                password System.env.NEXUS_PASS
            }
            url "https://nexusng.tuenti.io/repository/maven-release-private/"
        }
    }
    publications {
        impl(MavenPublication) {
            artifact 'build/outputs/aar/library-release.aar'
            artifact(sourceJar)
            groupId 'com.telefonica.mistica'
            artifactId 'mistica'
            version version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}