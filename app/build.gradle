plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.betomorrow.appcenter'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.telefonica.mistica.app"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode versionCodeDate()
        versionName "1.0.0"

        buildConfigField "String", "APPCENTER_KEY", "\"" + System.env.APPCENTER_KEY + "\""
    }

    lintOptions {
        warningsAsErrors true
    }
}

static def versionCodeDate() {
    return (new Date().toTimestamp().getTime() / 60).toInteger()
}

appcenter {
    apiToken = System.env.APPCENTER_API_TOKEN ?: ""
    ownerName = "Tuenti-Organization"
    distributionGroups = ["Public"]
    releaseNotes = "# Library version: ${version} \n See https://github.com/Telefonica/mistica-android/releases for more info."

    notifyTesters = true
    apps {
        debug {
            appName = "mistica"
        }
    }
    uploadMappingFiles = false
}

dependencies {
    def appCenterSdkVersion = '4.3.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    
    implementation project(':catalog')
    implementation project(':catalog-compose')
    implementation project(':library')

    testImplementation 'junit:junit:4.13'
}
